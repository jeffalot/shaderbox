<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="keywords" content="WebGL, shader, three.js, javaScript, example, 3D">
<meta name="description" content="sphere shader example">
<meta name="author" content="Javier Gracia Carpio">

<title>sphere shader example</title>

<link rel="icon" type="image/x-icon" href="/favicon.ico">

<!-- CSS files -->
<link rel="stylesheet" href="/shaderbox/css/styles.css">

<!-- JavaScript files -->
<script type="text/javascript" src="/shaderbox/js/libs/three.min.js"></script>
<script type="text/javascript" src="/shaderbox/js/libs/stats.min.js"></script>
<script type="text/javascript" src="/shaderbox/js/libs/dat.gui.min.js"></script>
<script type="text/javascript" src="/shaderbox/js/libs/OrbitControls.js"></script>
</head>

<body>
	<!-- Navigation bar -->
	<!-- <header class="nav-container">
		<h1 class="nav-header">
			<a href="/index.html" class="nav-item is-header-item">WebGL-shaders</a>
		</h1>

		<nav class="nav-menu-wrapper">
			<span class="nav-item is-menu-item menu-icon" onclick="return false;"></span>

			<ul class="nav-menu-list">
				<li><a href="/index.html" class="nav-item">Back to list</a></li>
				<li><a href="/js/shader-example-sphere.js" class="nav-item">JavaScript file</a></li>
				<li><a href="/shaders/vert-sphere.glsl" class="nav-item">Vertex shader</a></li>
				<li><a href="/shaders/frag-sphere.glsl" class="nav-item">Fragment shader</a></li>
			</ul>

			<ul class="nav-menu-list">
				<li><a href="/about.html" class="nav-item">About</a></li>
				<li><a href="https://github.com/jagracar/webgl-shader-examples" class="nav-item"><span class="github-icon"></span>
						Github</a></li>
			</ul>
		</nav>
	</header> -->

	<!-- Main content -->
	<main class="main-container">
	<article class="content">
		<div class="sketch-container" id="sketch-container">
			<div class="sketch-gui" id="sketch-gui"></div>
			<div class="sketch-stats" id="sketch-stats"></div>
		</div>

		<script type="x-shader/x-vertex" id="vertexShader">
#define GLSLIFY 1
// Common uniforms
uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;
uniform float u_frame;

// Common varyings
varying vec3 v_position;
varying vec3 v_normal;

/*
 * GLSL textureless classic 2D noise "cnoise",
 * with an RSL-style periodic variant "pnoise".
 * Author:  Stefan Gustavson (stefan.gustavson@liu.se)
 * Version: 2011-08-22
 *
 * Many thanks to Ian McEwan of Ashima Arts for the
 * ideas for permutation and gradient selection.
 *
 * Copyright (c) 2011 Stefan Gustavson. All rights reserved.
 * Distributed under the MIT license. See LICENSE file.
 * https://github.com/stegu/webgl-noise
 */

vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
    return mod289(((x * 34.0) + 1.0) * x);
}

vec4 taylorInvSqrt(vec4 r) {
    return 1.79284291400159 - 0.85373472095314 * r;
}

vec2 fade(vec2 t) {
    return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
}

float cnoise(vec2 P) {
    vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
    vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
    Pi = mod289(Pi);
    vec4 ix = Pi.xzxz;
    vec4 iy = Pi.yyww;
    vec4 fx = Pf.xzxz;
    vec4 fy = Pf.yyww;

    vec4 i = permute(permute(ix) + iy);

    vec4 gx = fract(i * (1.0 / 41.0)) * 2.0 - 1.0;
    vec4 gy = abs(gx) - 0.5;
    vec4 tx = floor(gx + 0.5);
    gx = gx - tx;

    vec2 g00 = vec2(gx.x, gy.x);
    vec2 g10 = vec2(gx.y, gy.y);
    vec2 g01 = vec2(gx.z, gy.z);
    vec2 g11 = vec2(gx.w, gy.w);

    vec4 norm = taylorInvSqrt(vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11)));
    g00 *= norm.x;
    g01 *= norm.y;
    g10 *= norm.z;
    g11 *= norm.w;

    float n00 = dot(g00, vec2(fx.x, fy.x));
    float n10 = dot(g10, vec2(fx.y, fy.y));
    float n01 = dot(g01, vec2(fx.z, fy.z));
    float n11 = dot(g11, vec2(fx.w, fy.w));

    vec2 fade_xy = fade(Pf.xy);
    vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
    float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
    return 2.3 * n_xy;
}

// The sphere radius
uniform float u_radius;

// Varying containing the sphere elevation
varying float v_elevation;

// Calculates the vertex displaced position
vec3 getDisplacedPosition(vec3 position) {
	// Calculate the vertex shift
	float shift = 2.0
			* cnoise(vec2(3.0 * cos(atan(position.z, position.x)), 2.0 * u_time + 3.0 * acos(position.y / u_radius)));

	return position + normal * shift;
}

/*
 * The main program
 */
void main() {
	// Calculate the new vertex position
	vec3 new_position = getDisplacedPosition(position);

	// Calculate the modelview position
	vec4 mv_position = modelViewMatrix * vec4(new_position, 1.0);

	// Save the varyings
	v_position = mv_position.xyz;
	v_elevation = length(new_position);

	// Vertex shader output
	gl_Position = projectionMatrix * mv_position;
}

		</script>

		<script type="x-shader/x-fragment" id="fragmentShader">
#define GLSLIFY 1
// Common uniforms
uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;
uniform float u_frame;

// Common varyings
varying vec3 v_position;
varying vec3 v_normal;

/*
 *  Calculates the normal vector at the given position
 *
 *  Uses this fix for some mobiles:
 *  https://stackoverflow.com/questions/20272272/standard-derivatives-from-fragment-shader-dfdx-dfdy-dont-run-correctly-in-a
 */
vec3 calculateNormal(vec3 position) {
    vec3 fdx = vec3(dFdx(position.x), dFdx(position.y), dFdx(position.z));
    vec3 fdy = vec3(dFdy(position.x), dFdy(position.y), dFdy(position.z));
    vec3 normal = normalize(cross(fdx, fdy));

    if (!gl_FrontFacing) {
        normal = -normal;
    }

    return normal;
}

/*
 *  Calculates the diffuse factor produced by the light illumination
 */
float diffuseFactor(vec3 normal, vec3 light_direction) {
    float df = dot(normalize(normal), normalize(light_direction));

    if (gl_FrontFacing) {
        df = -df;
    }

    return max(0.0, df);
}

// The sphere radius
uniform float u_radius;

// Varying containing the sphere elevation
varying float v_elevation;

/*
 * The main program
 */
void main() {
	// Calculate the new normal vector
	vec3 new_normal = calculateNormal(v_position);

	// Use the mouse position to define the light direction
	float min_resolution = min(u_resolution.x, u_resolution.y);
	vec3 light_direction = -vec3((u_mouse - 0.5 * u_resolution) / min_resolution, 0.25);

	// Set the default surface color
	vec3 surface_color = vec3(clamp(0.0, 1.0, 5.0*(v_elevation - u_radius)/u_radius), 0.3, 0.3);

	// Apply the light diffusion factor
	surface_color *= diffuseFactor(new_normal, light_direction);

	// Fragment shader output
	gl_FragColor = vec4(surface_color, 1.0);
}

		</script>

		<script type="text/javascript" src="/js/shader-example-sphere.js"></script>
	</article>
	</main>
</body>
</html>